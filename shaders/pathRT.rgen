#version 460
#extension GL_NV_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : require


// ----------------------------------------------------------------------------
//  Binding locations
//

layout(binding = 0, set = 0) uniform accelerationStructureNV topLevelAS;
layout(binding = 1, set = 0, rgba16) uniform image2D image;

layout(binding = 2, set = 0) uniform UBO
{
    mat4 model;
    mat4 view;
    mat4 proj;
    mat4 modelIT;
    mat4 viewProjInverse;

    mat4 lightTransform;

    vec2 lightSize;
    vec2 pad0;

    vec3  lightE;
    float pad1;

    int   numIndirectBounces;
    int   samplesPerPixel;
    float lightSourceArea;
    float lightOtherE;

    int   numAArays;
    float filterRadius;

    int   numAOrays;
    float aoRayLength;
    uint  iteration;

    float time;
}
ubo;

layout(binding = 3, set = 0) buffer Vertices
{
    vec4 v[];
}
vertices;

layout(binding = 4, set = 0) buffer Indices
{
    uint i[];
}
indices;

layout(binding = 5, set = 0) buffer MatColorBufferObject
{
    vec4 m[];
}
materials;

layout(binding = 6, set = 0) uniform sampler2D[] textureSamplers;

layout(binding = 7, set = 0) uniform sampler2DArray scrambleSampler;
layout(binding = 8, set = 0) buffer SobolMatrices
{
    uint sm[];
}
sobolMatrices;

// ----------------------------------------------------------------------------
//  Attribute locations
//

struct RayPayload
{
    vec3 barycentrics;
    uint primitiveID;
};

layout(location = 0) rayPayloadNV RayPayload payload;
layout(location = 2) rayPayloadNV bool isShadowed;

#define M_PI 3.141592653589
#define M_2PI 2.0 * M_PI
#define INV_PI 1.0 / M_PI


// ----------------------------------------------------------------------------
//
//

struct Vertex
{
    vec3 pos;
    vec3 normal;
    vec2 texCoord;
    vec3 color;
    int  matIndex;
};
// Number of vec4 values used to represent a vertex
uint vertexSize = 3;

Vertex unpackVertex(uint index)
{
    Vertex v;

    vec4 d0 = vertices.v[vertexSize * index + 0];
    vec4 d1 = vertices.v[vertexSize * index + 1];
    vec4 d2 = vertices.v[vertexSize * index + 2];

    v.pos      = d0.xyz;
    v.normal   = vec3(d0.w, d1.x, d1.y);
    v.texCoord = vec2(d1.z, d1.w);
    v.color    = vec3(d2.x, d2.y, d2.z);
    v.matIndex = floatBitsToInt(d2.w);
    return v;
}

// ----------------------------------------------------------------------------
//
//

struct WaveFrontMaterial
{
    vec3  ambient;
    vec3  diffuse;
    vec3  specular;
    vec3  transmittance;
    vec3  emission;
    float shininess;
    float metallic;
    float ior;       // index of refraction
    float dissolve;  // 1 == opaque; 0 == fully transparent
    int   illum;     // illumination model (see http://www.fileformat.info/format/material/)
    int   diffuseTextureId;
    int   specularTextureId;
    int   normalTextureId;
    float pad;
};
// Number of vec4 values used to represent a material
const int sizeofMat = 6;

WaveFrontMaterial unpackMaterial(int matIndex)
{
    WaveFrontMaterial m;
    vec4              d0 = materials.m[sizeofMat * matIndex + 0];
    vec4              d1 = materials.m[sizeofMat * matIndex + 1];
    vec4              d2 = materials.m[sizeofMat * matIndex + 2];
    vec4              d3 = materials.m[sizeofMat * matIndex + 3];
    vec4              d4 = materials.m[sizeofMat * matIndex + 4];
    vec4              d5 = materials.m[sizeofMat * matIndex + 5];

    m.ambient           = vec3(d0.x, d0.y, d0.z);
    m.diffuse           = vec3(d0.w, d1.x, d1.y);
    m.specular          = vec3(d1.z, d1.w, d2.x);
    m.transmittance     = vec3(d2.y, d2.z, d2.w);
    m.emission          = vec3(d3.x, d3.y, d3.z);
    m.shininess         = d3.w;
    m.metallic          = d4.x;
    m.ior               = d4.y;
    m.dissolve          = d4.z;
    m.illum             = int(d4.w);
    m.diffuseTextureId  = floatBitsToInt(d5.x);
    m.specularTextureId = floatBitsToInt(d5.y);
    m.normalTextureId   = floatBitsToInt(d5.z);
    return m;
}


// ----------------------------------------------------------------------------
//
//

mat3 formBasis(vec3 n)
{
    mat3 R;
    vec3 T, B;
    if(n.z < -0.9999999f)
    {
        T = vec3(0.0, -1.0, 0.0);
        B = vec3(-1.0, 0.0, 0.0);
    }
    else
    {
        const float a = 1.0f / (1.0f + n.z);
        const float b = -n.x * n.y * a;
        T             = vec3(1.0f - n.x * n.x * a, b, -n.x);
        B             = vec3(b, 1.0f - n.y * n.y * a, -n.y);
    }

    R[0] = T;
    R[1] = B;
    R[2] = n;
    return R;
}

// ----------------------------------------------------------------------------
//
//
vec3 localToWorld(const vec3 v, const vec3 normal)
{
    return formBasis(normal) * v;
}

// ----------------------------------------------------------------------------
//
//

float maxcoord(vec3 v)
{
    return max(max(v.x, v.y), v.z);
}

// ----------------------------------------------------------------------------
//
//

float sobol1DSample(uint index, const uint dimension, const uint scramble)
{
    // These values are from the sobol implementation from sobol.h/cpp
    const uint dimensions = 1024;
    const uint size       = 52;

    uint result = scramble;
    for(uint i = dimension * size; index != 0; index >>= 1, ++i)
    {
        if(uint(index & 1) == 1)
            result ^= sobolMatrices.sm[i];
    }

    //return result * (1.0 / (uint(1) << 32));
    return result * 2.3283064365386963e-10;
}

// ----------------------------------------------------------------------------
//
//

// Cosine weighed hemisphere sample based on shirley-chiu mapping
vec3 hemisphereSample(uint index, uvec2 scramble)
{
    vec2 s;
    for(int d = 0; d < 2; ++d)
    {
        s[d] = sobol1DSample(index, d, scramble[d]);
    }

    float       phi, r;
    const float a = 2.0 * s.x - 1.0;
    const float b = 2.0 * s.y - 1.0;

    if(a * a > b * b)
    {
        r   = a;
        phi = M_PI * 0.25 * (b / a);
    }
    else
    {
        r   = b;
        phi = M_PI * 0.5 - M_PI * 0.25 * (a / b);
    }

    float x = r * cos(phi);
    float y = r * sin(phi);
    float z = sqrt(max(0.0, 1.0 - x * x - y * y));

    return vec3(x, y, z);
}

// ----------------------------------------------------------------------------
//
//

vec3 hemisphereSample2(vec2 s)
{
    float       phi, r;
    const float a = 2.0 * s.x - 1.0;
    const float b = 2.0 * s.y - 1.0;

    if(a * a > b * b)
    {
        r   = a;
        phi = M_PI * 0.25 * (b / a);
    }
    else
    {
        r   = b;
        phi = M_PI * 0.5 - M_PI * 0.25 * (a / b);
    }

    float x = r * cos(phi);
    float y = r * sin(phi);
    float z = sqrt(max(0.0, 1.0 - x * x - y * y));

    return vec3(x, y, z);
}

// ----------------------------------------------------------------------------
//
//

void sampleLight(inout float pdf, inout vec3 p, vec2 prng)
{
    pdf      = 1.0 / (4.0 * ubo.lightSize.x * ubo.lightSize.y);
    vec2 pos = (prng * vec2(2.0) - vec2(1.0)) * ubo.lightSize;
    p        = vec4(ubo.lightTransform * vec4(pos.xy, 0.0, 1.0)).xyz;
}

// ----------------------------------------------------------------------------
//
//

float rand(vec2 co)
{
    return fract(sin(dot(co.xy, vec2(12.9898, 78.233))) * 43758.5453);
}

// ----------------------------------------------------------------------------
//  Cook - Torrance BSDF
//  DFG / (4 * dot(Wi, n) * dot(Wo, n)
//


float GGX_chi(float v)
{
    return v > 0.0 ? 1.0 : 0.0;
}

float GGX_Distribution(vec3 wm, float alpha)
{
    float a2    = alpha * alpha;
    float MdotN = wm.z;
    float chi   = GGX_chi(MdotN);
    float cos2  = MdotN * MdotN;
    float tan2  = (1.0 - cos2) / cos2;

    float denom = M_PI * pow(MdotN, 4.0) * pow(a2 + tan2, 2.0);
    return chi * a2 / denom;
}

float GGX_G1(vec3 wv, vec3 wm, float alpha)
{
    float a2    = alpha * alpha;
    float VdotM = dot(wv, wm);
    float VdotN = wv.z;

    float VdotM2 = VdotM * VdotM;
    float tan2   = (1.0 - VdotM2) / VdotM2;

    float chi   = GGX_chi(VdotM / VdotN);
    float denom = 1.0 + sqrt(1.0 + a2 * tan2);

    return chi * 2.0 / denom;
}

float GGX_Geometry(vec3 wi, vec3 wo, vec3 wm, float alpha)
{
    return GGX_G1(wi, wm, alpha) * GGX_G1(wo, wm, alpha);
}

float GGX_Fresnel(vec3 wo, vec3 wm, float etaA, float etaB)
{
    float OdotM = max(dot(wo, wm), 0.0);
    float eta   = etaA / etaB;
    float eta2  = eta * eta;

    float beta = sqrt(1.0 / eta2 + OdotM * OdotM - 1.0);

    // In case of total internal reflection, set F = 1.0
    if(beta < 0.0)
    {
        return 1.0;
    }

    float Rs = pow((OdotM - beta) / (OdotM + beta), 2.0);
    float Rp = pow((eta2 * beta - OdotM) / (eta2 * beta + OdotM), 2.0);

    return 0.5 * (Rs + Rp);
}

vec3 GGX_Sample(vec2 rnd, float alpha)
{
    float theta = atan(alpha * sqrt(rnd.x) / sqrt(1.0 - rnd.x));
    float phi   = M_2PI * rnd.y;

    float x = sin(theta) * cos(phi);
    float y = sin(theta) * sin(phi);
    float z = cos(theta);

    return vec3(x, y, z);
}

vec3 GGX_Sample2(vec2 rnd, float alpha)
{
    float a2    = alpha * alpha;
    float theta = acos(sqrt((1.0 - rnd.x) / ((a2 - 1.0) * rnd.x + 1.0)));
    float phi   = 2.0 * M_PI * rnd.y;

    float x = sin(theta) * cos(phi);
    float y = sin(theta) * sin(phi);
    float z = cos(theta);

    return vec3(x, y, z);
}

float GGX_PDF(vec3 wm, float alpha)
{
    //float a2       = alpha * alpha;
    //float cosTheta = abs(dot(m, n));
    //float sinTheta = sqrt(1.0 - cosTheta * cosTheta);
    //float denom    = M_PI * pow((cosTheta * cosTheta * (a2 - 1.0) + 1.0), 2.0);
    //return a2 * sinTheta * cosTheta / denom;

    float D = GGX_Distribution(wm, alpha);
    return D * abs(wm.z);
}

float GGX_Eval(vec3 wi, vec3 wo, vec3 wm, float alpha, float etaA, float etaB)
{
    float D = GGX_Distribution(wm, alpha);
    float G = GGX_Geometry(wi, wo, wm, alpha);
    float F = GGX_Fresnel(wo, wm, etaA, etaB);

    float IdotN = abs(wi.z);
    float OdotN = abs(wo.z);

    return D * G * F / (4.0 * IdotN * OdotN);
}
// ----------------------------------------------------------------------------
//
//

float GGX_FresnelDielectric(float cosThetaI, float ni, float nt)
{
    cosThetaI = clamp(cosThetaI, -1.0, 1.0);

    if(cosThetaI < 0.0)
    {
        float temp = ni;
        ni         = nt;
        nt         = temp;

        cosThetaI = -cosThetaI;
    }

    float sinThetaI = sqrt(max(0.0, 1.0 - cosThetaI * cosThetaI));
    float sinThetaT = ni / nt * sinThetaI;

    if(sinThetaI >= 1.0)
    {
        return 1.0;
    }

    float cosThetaT = sqrt(max(0.0, 1.0 - sinThetaT * sinThetaT));

    float rParallel = ((nt * cosThetaI) - (ni * cosThetaT)) / ((nt * cosThetaI) + (ni * cosThetaT));
    float rPerpendicular =
        ((ni * cosThetaI) - (nt * cosThetaT)) / ((ni * cosThetaI) + (nt * cosThetaT));

    return (rParallel * rParallel + rPerpendicular * rPerpendicular) * 0.5;
}

vec3 GGX_SchlickFresnel(vec3 r0, float radians)
{
    float expo = pow(1.0 - radians, 5.0);
    return r0 + (1.0 - r0) * expo;
}

float GGX_SmithMasking(vec3 wi, vec3 wo, float alpha)
{
    float a2    = alpha * alpha;
    float NdotV = abs(wo.z);
    float denom = sqrt(a2 + (1.0 - a2) * NdotV * NdotV) + NdotV;

    return 2.0 * NdotV / denom;
}

float GGX_SmithMaskingShadowing(vec3 wi, vec3 wo, float alpha)
{
    float a2    = alpha * alpha;
    float NdotL = wi.z;
    float NdotV = wo.z;

    float denomA = NdotV * sqrt(a2 + (1.0 - a2) * NdotL * NdotL);
    float denomB = NdotL * sqrt(a2 + (1.0 - a2) * NdotV * NdotV);

    return 2.0 * NdotL * NdotV / clamp(denomA + denomB, 1e-4, 1.0);
}

vec3 GGX_SampleVNDF(vec3 wo, float alpha, vec2 rnd)
{
    vec3 v = normalize(vec3(wo.x * alpha, wo.y * alpha, wo.z));

    vec3 t1 = (v.z < 0.9999) ? normalize(cross(v, vec3(0, 0, 1))) : vec3(1, 0, 0);
    vec3 t2 = cross(t1, v);

    float a   = 1.0 / (1.0 + v.z);
    float r   = sqrt(rnd.x);
    float phi = (rnd.y < a) ? rnd.y / a * M_PI : M_PI + (rnd.y - a) / (1.0 - a) * M_PI;
    float p1  = r * cos(phi);
    float p2  = r * sin(phi) * ((rnd.y < a) ? 1.0 : v.z);

    vec3 n = p1 * t1 + p2 * t2 + sqrt(max(0.0, 1.0 - p1 * p1 - p2 * p2)) * v;

    n = normalize(vec3(alpha * n.x, alpha * n.y, max(0.0, n.z)));
    return n;
}

bool refract(vec3 wi, vec3 wn, inout vec3 wt, float eta)
{
    float cosThetaI  = dot(wn, wi);
    float sin2ThetaI = max(0.0, 1.0 - cosThetaI * cosThetaI);
    float sin2ThetaT = eta * eta * sin2ThetaI;

    if(sin2ThetaT >= 1.0)
    {
        return false;
    }

    float cosThetaT = sqrt(1.0 - sin2ThetaT);
    wt              = eta * (-wi) + (eta * cosThetaI - cosThetaT) * wn;

    return true;
}

//vec3 GGX_ImportanceSampleVNDF(vec3)


// ----------------------------------------------------------------------------
//
//

struct Ray
{
    vec3 origin;
    vec3 dir;
};

Ray getPrimaryRay(vec2 var)
{
    const vec2 pixelCenter = vec2(gl_LaunchIDNV.xy) + var;
    const vec2 inUV        = pixelCenter / vec2(gl_LaunchSizeNV.xy);
    vec2       d           = inUV * 2.0 - 1.0;

    mat4 invP = ubo.viewProjInverse;

    // Point on front plane in homogeneous coordinates
    vec4 p0 = vec4(d.xy, 0.0, 1.0);
    // Point on back plane in homogeneous coordinates
    vec4 p1 = vec4(d.xy, 1.0, 1.0);

    // apply inverse projection, divide by w to get object-space points
    vec4 Roh = invP * p0;
    vec3 Ro  = vec4(Roh * (1.0 / Roh.w)).xyz;
    vec4 Rdh = invP * p1;
    vec3 Rd  = vec4(Rdh * (1.0 / Rdh.w)).xyz;

    // Subtract front plane from back plane
    Rd = Rd - Ro;

    Ray ray;
    ray.origin = Ro;
    ray.dir    = Rd;
    return ray;
}

float getGaussianWeight(vec2 offset, float stdDev)
{
    const float f = 1.0 / (stdDev * sqrt(2.0 * M_PI));
    return f * exp(-0.5 * (offset.y * offset.y + offset.x * offset.x) / (stdDev * stdDev));
}

float mitchellNetrevali(float v)
{
    // B + 2*C = 1
    const float B = 1.0 / 3.0;
    const float C = 1.0 / 3.0;
    float       k = 0.0;
    if(v < 1.0)
    {
        return ((12.0 - 9.0 * B - 6.0 * C) * pow(v, 3.0)
                + (-18.0 + 12.0 * B + 6.0 * C) * pow(v, 2.0) + (6.0 - 2.0 * B))
               / 6.0;
    }
    else
    {
        return ((-B - 6.0 * C) * pow(v, 3.0) + (6.0 * B + 30.0 * C) * pow(v, 2.0)
                + (-12.0 * B - 48.0 * C) * v + (8.0 * B + 24.0 * C))
               / 6.0;
    }
}

float getMitchellWeight(vec2 offset)
{
    return mitchellNetrevali(offset.x) * mitchellNetrevali(offset.y);
}

vec2 nextSquareSample(uint index, inout uint dim, uvec2 scramble)
{
    vec2 s;
    s[0] = sobol1DSample(index, dim++, scramble[0]);
    s[1] = sobol1DSample(index, dim++, scramble[1]);
    return s;
}

// ----------------------------------------------------------------------------
//
//



// ----------------------------------------------------------------------------
//
//

void main()
{

    const vec2 pixelCenter = vec2(gl_LaunchIDNV.xy) + vec2(0.5);
    const vec2 inUV        = pixelCenter / vec2(gl_LaunchSizeNV.xy);

    uint sobolIndex =
        floatBitsToUint(vec4(texelFetch(scrambleSampler, ivec3(gl_LaunchIDNV.xy, 0), 0)).r);
    sobolIndex /= 2;

    sobolIndex += ubo.iteration;
    uint sobolDim = 0;

    // scrambleSampler holds 32-layer texture array containing random uint values
    uvec2 scrambleArray[16];
    for(int i = 0; i < 16; i++)
    {
        scrambleArray[i][0] = floatBitsToUint(
            vec4(texelFetch(scrambleSampler, ivec3(gl_LaunchIDNV.xy, 2 * i + 0), 0)).r);
        scrambleArray[i][1] = floatBitsToUint(
            vec4(texelFetch(scrambleSampler, ivec3(gl_LaunchIDNV.xy, 2 * i + 1), 0)).r);
    }

    const float tmin       = 0.000001;
    const float tmax       = 1.0;
    const uint  rayFlags   = gl_RayFlagsOpaqueNV;
    const uint  cullMask   = 0xff;
    const int   maxBounces = ubo.numIndirectBounces;
    vec4        E          = vec4(0.0);

    if(ubo.iteration > 1)
    {
        E = imageLoad(image, ivec2(gl_LaunchIDNV.xy));
    }

    for(int aaRay = 0; aaRay < ubo.numAArays; ++aaRay)
    {
        Ray ray;

        vec2  rayOffset          = vec2(0.5);
        vec3  throughput         = vec3(1.0);
        float p                  = 1.0;
        int   bounce             = 0;
        uint  scrambleArrayLayer = 1;
        sobolDim                 = 2;
        float radius             = ubo.filterRadius;

        rayOffset = nextSquareSample(sobolIndex, sobolDim, scrambleArray[0]);
        if(ubo.numAArays == 1)
        {

            rayOffset = rayOffset * 2.0 - 1.0;
            rayOffset *= ubo.filterRadius;
            ray = getPrimaryRay(rayOffset + vec2(0.5));
            //ray = getPrimaryRay(vec2(0.5));
        }
        else
        {
            //do
            {
                //rayOffset = rayOffset * vec2(2.0) - vec2(1.0);
                //rayOffset *= radius;
            }
            //} while(rayOffset.x * rayOffset.x + rayOffset.y * rayOffset.y < radius * radius);

            ray = getPrimaryRay(rayOffset);
        }

        vec3 Ro = ray.origin;
        vec3 Rd = ray.dir;

        traceNV(topLevelAS, rayFlags, cullMask, 0, 0, 0, Ro, tmin, Rd, tmax, 0);
        if(payload.primitiveID == ~0u)
        {
            imageStore(image, ivec2(gl_LaunchIDNV.xy), vec4(inUV, 0.4, 1.0));
            return;
        }

        while(true)
        {
            uint  primitiveID  = payload.primitiveID;
            vec3  barycentrics = payload.barycentrics;
            uvec2 scramble     = scrambleArray[scrambleArrayLayer++];

            ivec3 ind = ivec3(indices.i[3 * primitiveID], indices.i[3 * primitiveID + 1],
                              indices.i[3 * primitiveID + 2]);

            Vertex v0 = unpackVertex(ind.x);
            Vertex v1 = unpackVertex(ind.y);
            Vertex v2 = unpackVertex(ind.z);

            WaveFrontMaterial mat = unpackMaterial(v1.matIndex);

            // Shading normal
            vec3 sNormal = normalize(v0.normal * barycentrics.x + v1.normal * barycentrics.y
                                     + v2.normal * barycentrics.z);

            // Geometric normal
            if(dot(sNormal, Rd) > 0.0)
            {
                sNormal = -sNormal;
            }
            const vec3 gNormal = sNormal;

            vec3 hitPoint =
                v0.pos * barycentrics.x + v1.pos * barycentrics.y + v2.pos * barycentrics.z;


            // Get color data
            vec3 albedo   = mat.diffuse;
            vec3 specular = mat.specular;
            vec3 textureN = vec3(1.0);

            if(mat.diffuseTextureId >= 0)
            {
                vec2 texCoord = v0.texCoord * barycentrics.x + v1.texCoord * barycentrics.y
                                + v2.texCoord * barycentrics.z;
                albedo *= texture(textureSamplers[mat.diffuseTextureId], texCoord).xyz;
            }
            if(mat.specularTextureId >= 0)
            {
                vec2 texCoord = v0.texCoord * barycentrics.x + v1.texCoord * barycentrics.y
                                + v2.texCoord * barycentrics.z;
                specular *= texture(textureSamplers[mat.specularTextureId], texCoord).xyz;
            }
            if(mat.normalTextureId >= 0)
            {
                vec2 texCoord = v0.texCoord * barycentrics.x + v1.texCoord * barycentrics.y
                                + v2.texCoord * barycentrics.z;
                textureN = texture(textureSamplers[mat.normalTextureId], texCoord).xyz * 2.0 - 1.0;
            }

            // Bump mapping
            if(mat.normalTextureId >= 0)
            {
                const vec3 v10 = v1.pos - v0.pos;
                const vec3 v20 = v2.pos - v0.pos;

                const vec2 uv10 = v1.texCoord - v0.texCoord;
                const vec2 uv20 = v2.texCoord - v0.texCoord;

                mat2 M;
                M[0] = uv10;
                M[1] = uv20;

                if(abs(determinant(M)) > 1e-4)
                {
                    M = inverse(M);

                    const vec2 col0 = M[0];
                    const vec2 col1 = M[1];

                    const vec3 T = normalize(v10 * col0.x + v20 * col0.y);
                    const vec3 B = normalize(v10 * col1.x + v20 * col1.y);

                    mat3 normalM;
                    normalM[0] = T;
                    normalM[1] = B;
                    normalM[2] = sNormal;

                    sNormal = normalM * textureN;
                }
            }


            {
                float max = maxcoord(albedo + specular);
                if(max > 1.0f)
                {
                    albedo /= vec3(max);
                    specular /= vec3(max);
                }
            }

            // costheta between surface normal and previous ray direction
            //float cosTheta_yi = clamp(dot(normalize(-Rd), sNormal), 0.0, 1.0);

            float pdf;
            vec3  lightSamplePos;
            {  // Sample light
                vec2 s = nextSquareSample(sobolIndex, sobolDim, scramble);
                sampleLight(pdf, lightSamplePos, s);
            }

            const vec3 vLight = lightSamplePos - hitPoint;

            const float roughness = clamp(1.0 - mat.shininess, 0.001, 1.0);
            float       alpha     = roughness * roughness;
            //alpha                 = float(ubo.numAOrays) / 256.0;

            const mat3 mLocalToWorld = formBasis(gNormal);
            const mat3 mWorldToLocal = transpose(mLocalToWorld);
            vec3       wo            = normalize(mWorldToLocal * (-Rd));

            vec3 Ei = vec3(0.0);

            // Trace shadowray to lightsource, invokes shadowmiss kernel
            isShadowed = true;
            traceNV(topLevelAS,
                    gl_RayFlagsTerminateOnFirstHitNV | gl_RayFlagsOpaqueNV
                        | gl_RayFlagsSkipClosestHitShaderNV,
                    0xFF, 1, 0, 1, hitPoint, tmin, vLight, tmax, 2);

            if(!isShadowed)
            {
                const vec3  lightNormal = -normalize(vec4(ubo.lightTransform[2]).xyz);
                const float r           = length(vLight);

                // Angle between light surface and vLight vector
                const float cosTheta_light = clamp(dot(normalize(-vLight), lightNormal), 0.0, 1.0);

                // Angle between hit surface and light vector
                const float cosTheta_surface = clamp(dot(sNormal, normalize(vLight)), 0.0, 1.0);

                // Half vector between raydir and vLight
                vec3 wi = normalize(mWorldToLocal * vLight);
                vec3 wm = normalize(wo + vLight);

                vec3  F  = GGX_SchlickFresnel(specular, dot(wi, wm));
                float G1 = GGX_SmithMasking(wi, wo, alpha);
                float G2 = GGX_SmithMaskingShadowing(wi, wo, alpha);

                vec3 brdf = vec3(F * (G2 / G1));

                Ei += ubo.lightE * cosTheta_light * cosTheta_surface / (r * r * pdf);
                Ei *= brdf * throughput;
            }

            vec3  wi             = vec3(0.0);
            vec3  wm             = vec3(0.0);
            vec3  specularBRDF   = vec3(0.0);
            float transmitFactor = 0.0;
            vec3 Kd             = 0.0;

            {
                vec2 rnd = nextSquareSample(sobolIndex, sobolDim, scramble);
                wm       = GGX_SampleVNDF(wo, alpha, rnd);

                float fres = GGX_FresnelDielectric(wo.z, 1.0, 1.45);
                Kd         = 1.0 - fres;
                if(mat.dissolve == 0.0)
                {

                    if(rnd.x < fres)
                    {
                        wi = normalize(reflect(-wo, wm));
                    }
                    else
                    {
                        //if(refract(wo, wm, wi, 1.0 / 1.45))
                        if(refract(wo, vec3(0, 0, 1), wi, 1.0 / 1.45))
                        {
                        }
                        else
                        {
                            wi = reflect(-wo, wm);
                        }
                        wi = normalize(wi);
                    }
                }
                else
                {
                    wi = normalize(reflect(-wo, wm));
                }

                if(wi.z > 0.0)
                {
                    if(mat.dissolve == 1.0)
                    {
                        vec3  F  = GGX_SchlickFresnel(specular, dot(wi, wm));
                        float G1 = clamp(GGX_SmithMasking(wi, wo, alpha), 1e-4, 1.0);
                        float G2 = GGX_SmithMaskingShadowing(wi, wo, alpha);

                        float D   = GGX_Distribution(wm, alpha);
                        float G1v = GGX_G1(wo, wm, alpha);

                        specular     = F * G1v;
                        specularBRDF = vec3(F * (G2 / G1));

                        p = G1v * abs(dot(wm, wi)) * D / wi.z;
                        p *= 1.0 / (4.0 * abs(dot(wo, wm)));
                    }
                    else
                    {
                        float F  = GGX_Fresnel(wo, wm, 1.0, 1.5);
                        float G1 = GGX_SmithMasking(wi, wo, alpha);
                        float G2 = GGX_SmithMaskingShadowing(wi, wo, alpha);

                        float D   = GGX_Distribution(wm, alpha);
                        float G1v = GGX_G1(wo, wm, alpha);

                        pdf          = 1.0 - clamp(fres, 0.0, 1.0 - 1e-2);
                        specularBRDF = albedo / pdf;

                        p = G1v * abs(dot(wm, wi)) * D / wi.z;
                        p *= 1.0 / (4.0 * abs(dot(wo, wm)));
                    }
                }
            }

            wi = mLocalToWorld * wi;

            Ro = hitPoint;
            Rd = wi * vec3(1000.0);

            //const vec3 diffuse = albedo * 1.0;
            const vec3 diffuseBRDF = albedo * INV_PI;
            vec4 color = vec4(Kd * diffuseBRDF + specularBRDF, 1.0);
            //vec4 color = vec4(diffuse + specular * specularBRDF, 1.0);

            // -----------------------
            // Filtering
            //if(ubo.numAArays != 1)
            //{
            //    float weight = getMitchellWeight(rayOffset);
            //    color *= vec4(weight);
            //}

            // -----------------------
            // Accumulate
            Ei += mat.emission * ubo.lightOtherE;
            Ei *= throughput * color.xyz;
            //Ei *= vec3(Kd * diffuseBRDF + throughput * specularBRDF);

            E += vec4(Ei, color.w);

            float cosTheta = clamp(dot(normalize(Rd), sNormal), 0.00001, 1.0);
            if(!(p < 0.0 || p >= 0.0))  // Test if NaN
            {
                //E.xyz = vec3(1e6, 0, 1e6);
                break;
            }
            if(p == 0.0)
            {
                //E.xyz = vec3(1e0, 1e6, 1e0);
                break;
            }
            throughput *= color.xyz;

            bounce++;
            if(bounce > maxBounces)
            {
                break;
            }

            traceNV(topLevelAS, rayFlags, cullMask, 0, 0, 0, Ro, tmin, Rd, tmax, 0);
            if(payload.primitiveID == ~0u)
            {
                break;
            }
        }

        sobolIndex++;
    }

    imageStore(image, ivec2(gl_LaunchIDNV.xy), E);
}